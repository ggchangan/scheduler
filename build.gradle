import org.apache.tools.ant.filters.ReplaceTokens

/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'magneto' at '17-7-11 下午2:14' with Gradle 2.12
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.12/userguide/tutorial_java_projects.html
 */
group 'cn.deepclue.scheduler'
version "$version"

buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

// Apply the java plugin to add support for Java
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'scheduler'
    version = "$version"
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5' //we want gradle 3.5 to run this project
}

// In this section you declare where to find the dependencies of your project
repositories {
    maven { url "http://$mavenIp:8081/nexus/content/groups/public" }
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

ext {
    // 运行和打包的环境选择, 默认是开发环境
    // 获取 gradle 参数中 -Dprofile 的值: gradle -Denv=prod clean build
    environment = System.getProperty("env", "dev")
    //dot(.) is not allow in database name, use backticks(`) also have propblem with not found `DBNAME`
    println "==> Load configuration for '" + environment + "'"
    println "==> Config environment, example: ./gradlew build -Denv=prod"
}

def loadConfiguration() {
    def configFile = file('config.groovy') // 配置文件
    return new ConfigSlurper(environment).parse(configFile.toURI().toURL()).toProperties()
}

allprojects {
    sourceSets {
        integrationTest {
            java.srcDir file('src/integTest/java')
            resources.srcDir file('src/integTest/resources')
            compileClasspath = sourceSets.main.output + configurations.testRuntime
            runtimeClasspath = output + compileClasspath
        }
    }
    task integrationTest(type: Test) {
        description = 'Runs the integration tests.'
        group = 'verification'
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
        reports.html.destination = file("$reports.html.destination/integration")
        reports.junitXml.destination = file("$reports.junitXml.destination/integration")
    }
    processResources {
        // src/main/resources 下的文件中 @key@ 的内容使用 config.groovy 里对应的进行替换
        from(sourceSets.main.resources.srcDirs) {
            include '*.yml', '*.properties'
            filter(ReplaceTokens, tokens: loadConfiguration())
        }
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.18'
    compile group: 'org.quartz-scheduler', name: 'quartz', version: "$quartzVersion"
    compile("org.springframework.boot:spring-boot-starter-web")
    compile "org.springframework.boot:spring-boot-starter-aop:$springBootVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile group: 'mysql', name: 'mysql-connector-java', version: "$mysqlDriverVersion"

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
}
